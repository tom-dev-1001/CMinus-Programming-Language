

const std = @import("std");
const struct_mod = @import("../core/structs.zig");
const enum_mod = @import("../core/enums.zig");
const error_mod = @import("../core/errors.zig");
const printing_mod = @import("../core/printing.zig");
const parse_utils_mod = @import("parse_utils.zig");
const Allocator = std.mem.Allocator;
const Token = struct_mod.Token;
const ArrayList = std.ArrayList;
const ParseData = struct_mod.ParseData;
const ParseError = error_mod.ParseError;
const TokenType = enum_mod.TokenType;
const print = std.debug.print;
const CompilerSettings = struct_mod.CompilerSettings;
const string = []const u8;


pub fn parseToTokens(allocator:Allocator, code:string, compiler_setting:*const CompilerSettings) !ArrayList(Token) {

    if (compiler_setting.output_to_file == false) {
        print("\t{s}Parsing{s}\t\t\t\t", .{printing_mod.GREY, printing_mod.RESET});
    }

    var token_list = try ArrayList(Token).initCapacity(allocator, 0);

    var parse_data:ParseData = .{
        .token_list = &token_list,
        .code = code,
        .compiler_settings = compiler_setting,
    };

    if (parse_data.code.len == 0) {
        return ParseError.Code_Length_Is_Zero;
    }

    const STRING_LENGTH:usize = code.len;
    while (parse_data.character_index < STRING_LENGTH) {

        try processCharacter(allocator, &parse_data);
    }
    if (compiler_setting.output_to_file == false) {
        print("{s}Done{s}\n", .{printing_mod.CYAN, printing_mod.RESET});
    }
    return token_list;
}

fn addCharCount(parse_data:*ParseData, character:u8) void {
    if (character == '\t') {
        parse_data.char_count += 3;
        return;
    }
    if (character == ' ' or character == '\\') {
        parse_data.char_count += 1;
    }
}

fn shouldSkip(allocator:Allocator, parse_data:*ParseData) !bool {

    if (parse_data.last_token != null) {

        if (parse_data.last_token.?.Type == TokenType.Comment) {
            parse_data.was_comment = true;
        }
    }

    const currentChar:u8 = parse_data.code[parse_data.character_index];
    addCharCount(parse_data, currentChar);

    if (currentChar == '\n') {

        if (parse_data.was_comment == true) {

            try parse_data.token_list.append(
                allocator,
                Token{
                    .Text = "", 
                    .Type = TokenType.EndComment, 
                    .LineNumber = parse_data.line_count, 
                    .CharNumber = parse_data.char_count
                },
            );
            parse_data.was_comment = false;
        }
        parse_data.line_count += 1;
        parse_data.char_count = 0;
        parse_data.character_index += 1;
        return true;
    }
    const is_special_char:bool =
        currentChar == '\r' or
        currentChar == '\t' or
        currentChar == ' ' or
        currentChar == '\\';

    if (is_special_char == true) {
        parse_data.character_index += 1;
        return true;
    }
    return false;
}

fn processCharacter(allocator:Allocator, parse_data:*ParseData) !void {

    if (try shouldSkip(allocator, parse_data) == true) {
        return;
    }

    const previous_character_index:usize = parse_data.character_index;
    const token:Token = try getToken(allocator, parse_data);

    if (previous_character_index == parse_data.character_index) {
        parse_data.character_index += 1;
    }

    try parse_data.token_list.append(allocator, token);
    parse_data.last_token = token;
}

fn getToken(allocator:Allocator, parse_data:*ParseData) !Token {

    const current_char:u8 = parse_data.code[parse_data.character_index];

    if (current_char == '"') {
        return readString(allocator, parse_data);
    }
    if (current_char == '\'') {
        return readChar(allocator, parse_data);
    }
    if (parse_utils_mod.isOperator(current_char)) {
        return readOperator(allocator, parse_data);
    }
    if (parse_utils_mod.isSeparator(current_char)) {
        return readSeparator(allocator, parse_data);
    }

    return readWord(allocator, parse_data);
}

fn readString(allocator:Allocator, parse_data:*ParseData) !Token {

    var text_builder:std.ArrayList(u8) = try std.ArrayList(u8).initCapacity(allocator, 0);

    //go past the '"'
    parse_data.*.character_index += 1;
    parse_data.char_count += 1;

    while (parse_data.character_index < parse_data.code.len) {

        const char:u8 = parse_data.code[parse_data.character_index];
        parse_data.char_count += 1;
        if (char == '"') {

            parse_data.character_index += 1;
            const text:[]u8 = try text_builder.toOwnedSlice(allocator);
            
            return Token{
                .Text = text,
                .Type = TokenType.StringValue,
                .LineNumber = parse_data.line_count,
                .CharNumber = parse_data.char_count,
            };
        }
        try text_builder.append(allocator, char);
        parse_data.character_index += 1;
    }

    return ParseError.Unterminated_String;
}

fn readSeparator(allocator:Allocator, parse_data: *ParseData) !Token {
    
    const char:[]u8 = try allocator.alloc(u8, 1);
    char[0] = parse_data.code[parse_data.character_index];
    const initial_char_count:usize = parse_data.char_count;

    parse_data.character_index += 1;
    const token_text:[]const u8 = char[0..];
    parse_data.char_count += token_text.len;
    return Token{
        .Text = token_text, 
        .Type = parse_utils_mod.getTokenType(token_text), 
        .LineNumber = parse_data.line_count, 
        .CharNumber = initial_char_count
    };
}

fn readChar(allocator:Allocator, parse_data:*ParseData) !Token {

    parse_data.character_index += 1;
    if (parse_data.character_index >= parse_data.code.len) {
        return ParseError.Unexpected_Value;
    }

    const char_value:[]u8 = try allocator.alloc(u8, 1);
    char_value[0] = parse_data.code[parse_data.character_index];
    parse_data.character_index += 1;

    if (parse_data.character_index >= parse_data.code.len) {
        return ParseError.Unexpected_Value;
    }

    if (parse_data.code[parse_data.character_index] != '\'') {
        return ParseError.Unterminated_Char;
    }
    parse_data.character_index += 1;

    return Token{
        .Text = char_value, 
        .Type = TokenType.CharValue, 
        .LineNumber = parse_data.line_count, 
        .CharNumber = parse_data.char_count
    };
}

fn readOperator(allocator:Allocator, parse_data:*ParseData) !Token {

    var text_builder:std.ArrayList(u8) = try std.ArrayList(u8).initCapacity(allocator, 0);
    const initial_char_count:usize = parse_data.char_count;

    const char:u8 = parse_data.code[parse_data.character_index];
    try text_builder.append(allocator, char);

    parse_data.character_index += 1;

    // Lookahead for compound operators like "==", "!="
    if (parse_data.character_index < parse_data.code.len) {

        const next:u8 = parse_data.code[parse_data.character_index];
        if (parse_utils_mod.isOperator(next)) {
            try text_builder.append(allocator, next);
            parse_data.character_index += 1;
        }
    }

    const text:[]u8 = try text_builder.toOwnedSlice(allocator);
    parse_data.char_count += text.len;
    return Token{
        .Text = text, 
        .Type = parse_utils_mod.getTokenType(text), 
        .LineNumber = parse_data.line_count, 
        .CharNumber = initial_char_count
    };
}

fn readWord(allocator:Allocator, parse_data:*ParseData) !Token {

    var text_builder:std.ArrayList(u8) = try std.ArrayList(u8).initCapacity(allocator, 0);
    const initial_char_count:usize = parse_data.char_count;

    while (parse_data.character_index < parse_data.code.len) {

        const c:u8 = parse_data.code[parse_data.character_index];

        if (parse_utils_mod.isLetterOrDigit(c) or c == '_') {
            try text_builder.append(allocator, c);
            parse_data.character_index += 1;
        } else {
            break;
        }
    }

    const text:[]u8 = try text_builder.toOwnedSlice(allocator);
    parse_data.char_count += text.len;
    return Token{
        .Text = text,
        .Type = parse_utils_mod.getTokenType(text),
        .LineNumber = parse_data.line_count, 
        .CharNumber = initial_char_count
    };
}